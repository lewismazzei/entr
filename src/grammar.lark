model: (entity_set | weak_entity_set | super_entity_set | relationship_set)*

entity_set: "ENT" entity_set_name ("EXTENDS" super_entity_set_name)? "{" attribute_list? "}"

weak_entity_set: "WEAK" "ENT" entity_set_name "IDENTIFIED" "BY" identifying_relationship_set_name ("EXTENDS" super_entity_set_name)? "{" attribute_list? "}"

super_entity_set: "SUPER" "ENT" completeness_constraint? disjointness_constraint? entity_set_name ("EXTENDS" super_entity_set_name)? "{" attribute_list? "}"

completeness_constraint: partial | total

disjointness_constraint: overlapping | disjoint

partial: "PARTIAL"

total: "TOTAL"

disjoint: "DISJOINT"

overlapping: "OVERLAP"

attribute_list: attribute ("," attribute)*

attribute: key_constraint? (basic_attribute_type | derived_multivalued_attribute_type) name
         | key_constraint? composite_multivalued_attribute_type? name?
         | key_constraint? composite_attribute_type name "{" attribute_list? "}"

key_constraint: "PRIMARY" -> primary
              | "DISCRIM" -> discriminator

basic_attribute_type: "MULTI"     -> multivalued
                    | "DERIVED"   -> derived

composite_attribute_type: "COMPOSITE" -> composite

composite_multivalued_attribute_type: "COMPOSITE" "MULTI" -> composite_multivalued
                                    | "MULTI" "COMPOSITE" -> composite_multivalued

derived_multivalued_attribute_type: "MULTI" "DERIVED" -> derived_multivalued
                                  | "DERIVED" "MULTI" -> derived_multivalued

relationship_set: "REL" name "{" participating_entity_set_list? ("ATTRIBUTES" "{" attribute_list "}")? "}"

participating_entity_set_list: participating_entity_set ("," participating_entity_set)*

participating_entity_set: name participation_constraint? cardinality_constraint? role?

participation_constraint: "PARTIAL" -> partial
                        | "TOTAL"   -> total

cardinality_constraint: "ONE"  -> one
                      | "MANY" -> many

// any valid identifier bar keywords (credit: https://stackoverflow.com/a/56100287)

name: /(?!(COMPOSITE|MULTI|DERIVED)\b)[a-z_][a-z0-9_]*/i

entity_set_name: /(?!(PARTIAL|TOTAL|OVERLAPPING|DISJOINT)\b)[a-z_][a-z0-9_]*/i

super_entity_set_name: /(?!(PARTIAL|TOTAL|OVERLAPPING|DISJOINT)\b)[a-z_][a-z0-9_]*/i

identifying_relationship_set_name: /(?!(PARTIAL|TOTAL|OVERLAPPING|DISJOINT)\b)[a-z_][a-z0-9_]*/i

role: IDENTIFIER

%import common.CNAME -> IDENTIFIER

%import common.WS
%ignore WS
